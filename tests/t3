// This file is a part of the Classp parser, formatter, and AST generator. 
// Description: Used to generate skeleton-generator.h from the skeleton files. 
//  
// Copyright 2015 Google Inc. 
//  
// Licensed under the Apache License, Version 2.0 (the "License"); 
// you may not use this file except in compliance with the License. 
// You may obtain a copy of the License at 
//  
// http://www.apache.org/licenses/LICENSE-2.0 
//  
// Unless required by applicable law or agreed to in writing, software 
// distributed under the License is distributed on an "AS IS" BASIS, 
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. 
// See the License for the specific language governing permissions and 
// limitations under the License. 
 

class ProcedureDecl {
  %parseable;
  identifier name;
  Declaration arguments[];
  Declaration locals[];
  Statement statements[];
  syntax(
    "procedure" name "(" ( arguments * "," ) ")"
    "declare"
      ( locals * ";" )
    "begin"
      ( statements * )
    "end");
  sample('procedure foo (int a) declare int b begin foo(a) end',
  '(ProcedureDecl name:foo arguments:[(Declaration type_name:int variable_name:a)] locals:[(Declaration type_name:int variable_name:b)] statements:[(Statement procedure_name:foo arguments:[a])])');
}

class Declaration {
  identifier type_name;
  identifier variable_name;
  syntax( type_name variable_name );
}

class Statement {
  identifier procedure_name;
  identifier arguments[];
  syntax( procedure_name "(" (arguments * "," ) ")" );
}

class Unparsed {
  int foo;
  syntax( foo );
}